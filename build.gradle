buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

allprojects {
    repositories {
        mavenCentral()
    }
}

def makeBuildId() {
    // If we're on Jenkins, use the build number as a tag. Otherwise use a
    // timestamp. BUILD_NUMBER will be set by Jenkins.
    def buildId = System.getenv()['BUILD_NUMBER']
    if (buildId == null) { // Not on jenkins
        def cal = Calendar.instance
        def format = new java.text.SimpleDateFormat('yyyyMMddHHmm')
        buildId = format.format(cal.time)
    }
    return buildId
}

/*
   buildTag = [version]-[versionCode].[buildId]
 */
allprojects {
    version = "0.1"
    def id = makeBuildId()
    ext {
        versionCode = 1
        buildId = versionCode + '.' + id
        buildTag = "$version-$buildId"
    }
}


